; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/

[Actions]
requiredWinstVersion >= "4.11.4.17"

include_insert "insert_check_exit_code.opsiinc"
include_append "section_linkfolder.opsiinc"

DefVar $MsiId$
DefVar $UninstallProgram$
DefVar $LogDir$
DefVar $ProductId$  
DefVar $MinimumSpace$
DefVar $InstallDir$
DefVar $PathToExe$
DefVar $DesktopIconName$
DefVar $PrettyName$
DefVar $PackageVersion$
DefVar $Version$
DefVar $msi_name$ 
DefVar $Property_DesktopIcon$
DefVar $CustomPostInstall$
DefVar $CustomPostDeinstall$
DefVar $LicenseType$
DefVar $DisableWelcomeScreen$
DefVar $DisableAutoCheckForLiveUpdate$
DefVar $DisableAddOnStore$
DefVar $DisableCeip$
DefVar $DisableCitaviAccount$
DefVar $DisableWordAddin$
DefVar $DeleteUserSettingsOnShutdown$

Set $LogDir$							 = "%opsiLogDir%"  
Set $msi_name$             = "Citavi6Setup.msi"
Set $MsiId$                = "{6A331045-8FF4-4BC9-9C56-E593ACAE28C2}"
Set $PackageVersion$	     = takeString(1, splitString ("%installingProdVersion%","-"))
Set $Version$	             = strPart("%installingProdVersion%", "1", calculate(strPos("%installingProdVersion%","-") + "-" + "1" ))
Set $Property_DesktopIcon$ = GetProductProperty("desktopicon","True")
Set $DeleteUserSettingsOnShutdown$ = GetProductProperty("delete-user-settings-on-shutdown","0")
Set $DisableAddOnStore$ = GetProductProperty("disable-add-on-store","False")
Set $DisableAutoCheckForLiveUpdate$ = GetProductProperty("disable-auto-check-for-live-update","False")
Set $DisableCeip$ = GetProductProperty("disable-ceip","False")
Set $DisableCitaviAccount$ = GetProductProperty("disable-citavi-account","False")
Set $DisableWelcomeScreen$ = GetProductProperty("disable-welcome-screen","False")
Set $DisableWordAddin$ = GetProductProperty("disable-word-addin","False")
; ----------------------------------------------------------------
Set $ProductId$       = "dfn_citavi"
Set $PrettyName$      = "Citavi"
Set $MinimumSpace$    = "600 MB"
Set $DesktopIconName$ = "Citavi 6"
Set $InstallDir$			= "%ProgramFiles32Dir%\Citavi 6"
Set $PathToExe$       = $InstallDir$ + "\bin\Citavi.exe"
Set $LicenseType$     = GetProductProperty("type-of-license","testversion")
; ----------------------------------------------------------------

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError "No Space"
	; Stop process and set installation status to failed
else
  comment "Show product picture"
	ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $PrettyName$ + " " + $Version$
	
  if $LicenseType$ = "site-license-file"
	  if (not(FileExists("%ScriptPath%\custom\common.dat6")))
      LogError "No common.dat6 site license file found in custom folder of this product, though kind-of-license property set to site-license-file. Please copy your common.dat4 file to the custom folder of your opsi server or change the type-of-license property to testversion."
      isFatalError "No common.dat6 license file found in custom folder, though kind-of-license property set to site-license-file" 
	  endif
	endif

	Killtask "Citavi.exe"
	
	if FileExists("%ScriptPath%\delsub.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub.opsiscript"
	endif
	
	Message "Installing " + $ProductId$ + " ..."	
	comment "Start setup program"
	ChangeDirectory "%SCRIPTPATH%"
	Winbatch_install
	Sub_check_exitcode
	
	comment "Site License File"
	if $LicenseType$ = "site-license-file"
    if (FileExists("%ScriptPath%\custom\common.dat6"))
      Files_copy_dat6_license
    endif
  endif

  comment "Citavi Admin Properties"
	Registry_Admin_Properties /32bit
	
	comment "Word Addin"
	if $DisableWordAddin$ = "True"
		comment "Disable Word Addin"
    Registry_Disable_Word_Addin /32bit
		Registry_Disable_Word_Addin /64bit
	;else
	; comment "Enable Word Addin"
		Registry_Enable_Word_Addin /32bit
		Registry_Enable_Word_Addin /64bit
  endif	

	comment "Desktop Icon"
	if $Property_DesktopIcon$ = "True"
    LinkFolder_create
  else
    LinkFolder_delete
  endif

  comment "include custom post install file"
  set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
  if not ($CustomPostInstall$ = "none")
	  if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
		  include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
	  endif
  endif

	comment "Test for installation success"
	;Test if software marked as installed in registry
    if (GetRegistryStringValueSysnative("[HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\" + $MsiId$ + "] DisplayName") = "")
        if (GetRegistryStringValue32("[HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\" + $MsiId$ + "] DisplayName") = "")
            logError "Fatal: After Installation [HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\" + $MsiId$ + "] not found"
            isFatalError "not_found_after_install"
        else
            comment "Successful Installation x86"
        endif
    endif

  ; logdatein bei bedarf ("$LogDir$\$ProductId$.install_log.txt" funktioniert nicht)
  ;includelog "c:\opsi.org\log\dfn_citavi.uninstall_log.txt" "1000" "auto"
  ;includelog "c:\opsi.org\log\dfn_citavi.install_log.txt" "1000" "auto"

endif

[Winbatch_install]
msiexec /i "%ScriptPath%\$msi_name$" /q INSTALLDESKTOPSHORTCUTS=0 INSTALLSTARTMENUSHORTCUTS=0 /l* "$LogDir$\$ProductId$.install_log.txt" ALLUSERS=1 REBOOT=ReallySuppress 

[Files_copy_dat6_license]
copy "%ScriptPath%\custom\common.dat6" "%ProgramFiles32Dir%\Citavi 6\bin"

[Registry_Admin_Properties]
OpenKey [HKLM\Software\Swiss Academic Software\Citavi 6\AdminSettings]
Set "DisableWelcomeScreen"="$DisableWelcomeScreen$"
Set "DisableAutoCheckForLiveUpdate"="$DisableAutoCheckForLiveUpdate$"
Set "DisableAddOnStore"="$DisableAddOnStore$"
Set "DisableCeip"="$DisableCeip$"
Set "DisableCitaviAccount"="$DisableCitaviAccount$"
Set "DeleteUserSettingsOnShutdown"="$DeleteUserSettingsOnShutdown$"

[Registry_Disable_Word_Addin]
OpenKey [HKLM\Software\Microsoft\Office\Word\Addins\SwissAcademic.Citavi.WordAddIn.Connect]
Set "LoadBehavior"=reg_dword:00000000
Set "Warmup"=reg_dword:00000000

[Registry_Enable_Word_Addin]
OpenKey [HKLM\Software\Microsoft\Office\Word\Addins\SwissAcademic.Citavi.WordAddIn.Connect]
Set "LoadBehavior"=reg_dword:00000003
Set "Warmup"=reg_dword:00000001
