; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/

[Actions]
requiredWinstVersion >= "4.11.4.17"

include_insert "insert_check_exit_code.opsiinc"
include_insert "insert_get_licensekey.opsiinc"
include_append "section_linkfolder.opsiinc"

DefVar $MsiId$
DefVar $UninstallProgram$
DefVar $LogDir$
DefVar $ProductId$  
DefVar $MinimumSpace$
DefVar $InstallDir$
DefVar $PathToExe$
DefVar $DesktopIconName$
DefVar $PrettyName$
DefVar $PackageVersion$
DefVar $Version$
DefVar $msi_name$ 
DefVar $Property_DesktopIcon$
DefVar $CustomPostInstall$
DefVar $CustomPostDeinstall$
DefVar $LicenseType$

Set $LogDir$							 = "%opsiLogDir%"  
Set $msi_name$             = "Citavi5Setup.msi"
Set $PackageVersion$	     = takeString(1, splitString ("%installingProdVersion%","-"))
Set $Version$	             = strPart("%installingProdVersion%", "1", calculate(strPos("%installingProdVersion%","-") + "-" + "1" ))
Set $Property_DesktopIcon$ = GetProductProperty("desktopicon","True")
; ----------------------------------------------------------------
Set $ProductId$       = "citavi"
Set $PrettyName$      = "Citavi"
Set $MinimumSpace$    = "600 MB"
Set $DesktopIconName$ = "Citavi 5"
Set $InstallDir$			= "%ProgramFiles32Dir%\Citavi 5"
Set $PathToExe$       = $InstallDir$ + "\bin\Citavi.exe"
Set $LicenseType$     = GetProductProperty("type-of-license","testversion")
Set $LicensePool$     = GetProductProperty("licensepool","none")

if ($LicensePool$ = "none")
  Set $LicenseRequired$ = "false"
else 
  Set $LicenseRequired$ = "true"
endif
; ----------------------------------------------------------------

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError "No Space"
	; Stop process and set installation status to failed
else
  comment "Show product picture"
	ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $PrettyName$ + " " + $Version$
	
  if $LicenseType$ = "site-license-file"
	  if ((not(FileExists("%ScriptPath%\custom\common.dat4"))) and (not(FileExists("%ScriptPath%\custom\common.dat5"))))
      LogError "No common.dat4 or common.dat5 site license file found in custom folder of this product, though kind-of-license property set to site-license-file. Please copy your common.dat4 file to the custom folder of your opsi server or change the type-of-license property to testversion."
      isFatalError "No common.dat4 or common.dat5 license file found in custom folder, though kind-of-license property set to site-license-file" 
	  endif
	endif

	if $LicenseRequired$ = "true"
		comment "Licensing required, reserve license and get license key"
		Sub_get_licensekey
	endif
	
	Killtask "Citavi.exe"

	
	if FileExists("%ScriptPath%\delsub.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub.opsiscript"
	endif
	
	Message "Installing " + $ProductId$ + " ..."	
	comment "Start setup program"
	ChangeDirectory "%SCRIPTPATH%"
	Winbatch_install
	Sub_check_exitcode
	
	if $LicenseType$ = "site-license-file"
	  if (FileExists("%ScriptPath%\custom\common.dat4"))
  	  Files_copy_dat4_license
    endif
    if (FileExists("%ScriptPath%\custom\common.dat5"))
      Files_copy_dat5_license
    endif
  endif

	if $Property_DesktopIcon$ = "True"
    LinkFolder_create
  else
    LinkFolder_delete
  endif

  comment "include custom post install file"
  set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
  if not ($CustomPostInstall$ = "none")
	  if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
		  include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
	  endif
  endif

endif

[Winbatch_install]
msiexec /i "%ScriptPath%\$msi_name$" /q INSTALLDESKTOPSHORTCUTS=1 INSTALLSTARTMENUSHORTCUTS=1 /l* "$LogDir$\$ProductId$.install_log.txt" ALLUSERS=1 REBOOT=ReallySuppress 

[Files_copy_dat4_license]
copy "%ScriptPath%\custom\common.dat4" "%ProgramFiles32Dir%\Citavi 5\bin"

[Files_copy_dat5_license]
copy "%ScriptPath%\custom\common.dat5" "%ProgramFiles32Dir%\Citavi 5\bin"
